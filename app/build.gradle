apply plugin: 'com.android.application'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.donnnno.arcticons"
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode 115
        versionName '6.6.0'
        multiDexEnabled true

        // Naming app bundles
        setArchivesBaseName("${parent.name}-v${versionName}")

    }

    buildTypes {
        release {
            minifyEnabled false
            debug {
                applicationIdSuffix '.debug'
            }
        }
    }


    flavorDimensions "variant"
    productFlavors {
        dark {
            dimension "variant"
            resValue "string", "app_name", "Arcticons Dark"
        }
        darkPlay {
            dimension "variant"
            resValue "string", "app_name", "Arcticons Dark"
        }
        light {
            dimension "variant"
            //noinspection GradlePath
            applicationIdSuffix '.light'
            resValue "string", "app_name", "Arcticons Light"
        }
        lightPlay {
            dimension "variant"
            applicationIdSuffix '.light'
            resValue "string", "app_name", "Arcticons Light"
        }
        you {
            dimension "variant"
            minSdkVersion "26"
            applicationIdSuffix '.you'
            resValue "string", "app_name", "Arcticons You"
        }
        youPlay {
            dimension "variant"
            minSdkVersion "26"
            applicationIdSuffix '.you'
            resValue "string", "app_name", "Arcticons You"
        }
    }

    sourceSets {
        dark { res.srcDirs = ['src/darkFree/res/', 'src/dark/res'] }
        darkPlay { res.srcDirs = ['src/darkPlay/res/', 'src/dark/res'] }
        light { res.srcDirs = ['src/lightFree/res/', 'src/light/res'] }
        lightPlay { res.srcDirs = ['src/lightPlay/res/', 'src/light/res'] }
		you { res.srcDirs = ['src/youFree/res/', 'src/you/res'] }
        youPlay { res.srcDirs = ['src/youPlay/res/', 'src/you/res'] }
    }


    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'com.donnnno.arcticons'
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.donnnno:candybar-foss:3.18.3'
}

import com.android.tools.profgen.ArtProfileKt
import com.android.tools.profgen.ArtProfileSerializer
import com.android.tools.profgen.DexFile


project.afterEvaluate {
    tasks.each { task ->
        if (task.name.startsWith("compile") && task.name.endsWith("ReleaseArtProfile")) {
            task.doLast {
                outputs.files.each { file ->
                    if (file.name.endsWith(".profm")) {
                        println("Sorting ${file} ...")
                        def version = ArtProfileSerializer.valueOf("METADATA_0_0_2")
                        def profile = ArtProfileKt.ArtProfile(file)
                        def keys = new ArrayList(profile.profileData.keySet())
                        def sortedData = new LinkedHashMap()
                        Collections.sort keys, new DexFile.Companion()
                        keys.each { key -> sortedData[key] = profile.profileData[key] }
                        new FileOutputStream(file).with {
                            write(version.magicBytes$profgen)
                            write(version.versionBytes$profgen)
                            version.write$profgen(it, sortedData, "")
                        }
                    }
                }
            }
        }
    }
}